module ctrl_ws_arr (

    input clk,
    input reset,
    input init_m,
    input done_led,
    input z,

    output reg init_led,
    output reg rst,
    output reg inc,
    output reg done
);

parameter START      = 3'b000;
parameter START_SEND = 3'b001;
parameter SEND_LED   = 3'b010;
parameter WAIT_TX    = 3'b011;
parameter INC        = 3'b100;
parameter CHECK_END  = 3'b101;
parameter END_SEND   = 3'b110;

reg [2:0] state;


always @(posedge clk) begin

    if(reset)
        state <= START;

    else begin

        case(state)

            START:
                if(init_m)
                    state <= START_SEND;
                else
                    state <= START;

            START_SEND:
                state <= SEND_LED;

            SEND_LED:
                state <= WAIT_TX;

            WAIT_TX:
                if(done_led)
                    state <= INC;
                else
                    state <= WAIT_TX;

            INC:
                state <= CHECK_END;

            CHECK_END:
                if(z)
                    state <= END_SEND;
                else
                    state <= START_SEND;

            END_SEND:
                state <= START;

            default:
                state <= START;

        endcase

    end

end

always @(*) begin

    init_led = 0;
    rst      = 0;
    inc      = 0;
    done     = 0;

    case(state)

        START: begin
            rst = 1;
        end

        START_SEND: begin
            init_led = 1;
        end

        SEND_LED: begin
        end

        WAIT_TX: begin
        end

        INC: begin
            inc = 1;
        end

        CHECK_END: begin
        end

        END_SEND: begin
            done = 1;
        end

    endcase

end


`ifdef BENCH
reg [8*40:1] state_name;

always @(*) begin

    case(state)

        START      : state_name = "START";
        START_SEND : state_name = "START_SEND";
        SEND_LED   : state_name = "SEND_LED";
        WAIT_TX    : state_name = "WAIT_TX";
        INC        : state_name = "INC";
        CHECK_END  : state_name = "CHECK_END";
        END_SEND   : state_name = "END_SEND";

    endcase

end
`endif

endmodule